let
  // ============================================================
  // PARÂMETROS
  // ============================================================
  
  // Data Inicial 
  DataInicial = #date(Date.Year(DateTime.LocalNow()) - 2, 1, 1),
  
  // Anos à frente (até final do ano atual + anosAdiante)
  anosAdiante = 2,
  // Data Final
  dataFim = Date.AddYears(Date.EndOfYear(Date.From(DateTime.LocalNow())), anosAdiante),
  
  // Lista de anos no intervalo
  intervaloAnos = List.Distinct(
            List.Transform(
                List.Dates(
                    DataInicial,
                    Duration.Days(dataFim - DataInicial) + 1,
                    #duration(1,0,0,0)
                ),
                each Date.Year(_)
            )
        ),
  
  // Lista contínua de todas as datas
  listaDatas = List.Dates(
            DataInicial,
            Duration.Days(dataFim - DataInicial) + 1,
            #duration(1,0,0,0)
        ),
  
  
  // ============================================================
  // FERIADOS FIXOS
  // ============================================================
  
  feriadosFixosBase = #table({"Dia", "Mes", "Feriado"},
        {
            {01, 01, "Confraternização Universal"},
            {20, 01, "São Sebastião"},
            {21, 04, "Tiradentes"},
            {23, 04, "São Jorge"},
            {01, 05, "Dia do Trabalhador"},
            {07, 09, "Independência do Brasil"},
            {12, 10, "Nossa Senhora Aparecida"},
            {02, 11, "Finados"},
            {15, 11, "Proclamação da República"},
            {20, 11, "Consciência Negra"},
            {25, 12, "Natal"}
        }),
  
  fnFeriadosFixos = (ano as number) =>
        Table.AddColumn(
            feriadosFixosBase,
            "Data",
            each #date(ano, [Mes], [Dia]),
            type date
        )[[Data],[Feriado]],
  
  feriadosFixos = Table.Combine(List.Transform(intervaloAnos, each fnFeriadosFixos(_))),
  
  
  // ============================================================
  // FERIADOS MÓVEIS (PÁSCOA ETC.)
  // ============================================================
  
  fnFeriadosMoveis = (ano as number) =>
    let
        modExcel = (x, y) =>
            let m = Number.Mod(x, y)
            in if m < 0 then m + y else m,

        pascoa =
            Date.From(
                Number.Round(
                    Number.From(#date(ano, 4, 1)) / 7 +
                    modExcel(19 * modExcel(ano, 19) - 7, 30) * 0.14,
                    0,
                    RoundingMode.Up
                ) * 7 - 6
            ),

        // deslocamento até a primeira segunda-feira de outubro
        deslocPrimeiraSegunda =
            Number.Mod(7 - Date.DayOfWeek(#date(ano, 10, 1), Day.Monday), 7),

        primeiraSegundaOutubro =
            Date.AddDays(#date(ano, 10, 1), deslocPrimeiraSegunda),

        terceiraSegundaOutubro =
            Date.AddDays(primeiraSegundaOutubro, 14),

        tabela =
            #table({"Data","Feriado"},
            {
                {Date.AddDays(pascoa, -47), "Carnaval"},
                {Date.AddDays(pascoa, -2), "Paixão de Cristo"},
                {pascoa, "Páscoa"},
                {Date.AddDays(pascoa, 60), "Corpus Christi"},
                {terceiraSegundaOutubro, "Dia do Trabalhador de Informática e TI"}
            })
    in tabela,
  
  feriadosMoveis = Table.Combine(List.Transform(intervaloAnos, each fnFeriadosMoveis(_))),
  
  
  // ============================================================
  // FERIADOS FINAL
  // ============================================================
  
  feriados = Table.Group(
            Table.Combine({feriadosFixos, feriadosMoveis}),
            {"Data"},
            {{"Feriado", each Text.Combine(_[Feriado], "/"), type text}}
        ),
  
  
  // ============================================================
  // TABELA DE DATAS
  // ============================================================
  
  tabelaDatas = Table.FromList(listaDatas, Splitter.SplitByNothing(), {"Data"}),
  
  // Índice
  add_Index = Table.AddIndexColumn(tabelaDatas, "IndiceData", 1, 1, Int64.Type),
  
  // Dia
  add_Dia = Table.AddColumn(add_Index, "Dia", each Date.Day([Data]), Int64.Type),
  
  // Dia da Semana
  add_DiaDaSemana = Table.AddColumn(add_Dia, "DiaDaSemana", each Text.Proper(Text.Start(Date.DayOfWeekName([Data], "pt-BR"), 3))),
  
  // Mes
  add_Mes = Table.AddColumn(add_DiaDaSemana, "Mes", each Text.Proper(Date.ToText([Data], "MMM", "pt-BR"))),
  
  // MesNum
  add_MesNum = Table.AddColumn(add_Mes, "MesNum", each Date.Month([Data]), Int64.Type),
  
  // Semana do Mês
  add_SemanaMes = Table.AddColumn(add_MesNum, "SemanaMes", each Date.WeekOfMonth([Data]), Int64.Type),
  
  // Semana do Ano
  add_SemanaAno = Table.AddColumn(add_SemanaMes, "SemanaAno", each Date.WeekOfYear([Data]), Int64.Type),
  
  // Trimestre
  add_Trimestre = Table.AddColumn(add_SemanaAno, "Trimestre", each Date.QuarterOfYear([Data]), Int64.Type),
  
  // Ano
  add_Ano = Table.AddColumn(add_Trimestre, "Ano", each Date.Year([Data]), Int64.Type),
  
  // Junta Feriados (left join)
  mergeFeriados = Table.NestedJoin(add_Ano, {"Data"}, feriados, {"Data"}, "Feriados", JoinKind.LeftOuter),
  expandeFeriados = Table.ExpandTableColumn(mergeFeriados, "Feriados", {"Feriado"}, {"Feriado"}),
  
  #"Personalização Adicionada" = Table.AddColumn(expandeFeriados, "SemMes", each 
  if Date.DayOfWeek([Data], Day.Monday) > 4 or [Feriado] <> null then null else
    let
        PrimeiroDiaMes = Date.StartOfMonth([Data]),
        DiasNoMes = List.Dates(PrimeiroDiaMes, Date.DaysInMonth(PrimeiroDiaMes), #duration(1,0,0,0)),
        DiasUteisSemFeriado = List.Select(DiasNoMes, each Date.DayOfWeek(_, Day.Monday) <= 4),
        DiasValidos = List.Select(DiasUteisSemFeriado, each Table.SelectRows(expandeFeriados, (r) => r[Data] = _ and r[Feriado] = null)[Data] <> {}),
        PrimeiroUtilValido = List.First(DiasValidos),
        SemanaInicio = Date.WeekOfYear(PrimeiroUtilValido, Day.Monday),
        SemanaAtual = Date.WeekOfYear([Data], Day.Monday),
        SemanaRelativa = SemanaAtual - SemanaInicio + 1
    in
        SemanaRelativa),
  
  
  // ============================================================
  // RESULTADO FINAL
  // ============================================================
  
  tabelaFinal = #"Personalização Adicionada",
  #"Transformar colunas" = Table.TransformColumnTypes(tabelaFinal, {
    {"Data", type date}, 
    {"DiaDaSemana", type text}, 
    {"Mes", type text}, 
    {"SemMes", Int64.Type}}),
  
  #"Substituir erros" = Table.ReplaceErrorValues(#"Transformar colunas", {{"Data", null}})
in
  #"Substituir erros"
